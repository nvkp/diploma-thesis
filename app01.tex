\chapter{SPARQL Queries}

\begin{lstlisting}[language = SPARQL, caption={Extracting the Wikidata's political data about the Czech Republic}, label={wdextract},captionpos=b, escapeinside={(*@}{@*)}]
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>

CONSTRUCT {
    ?area p:P31 wd:Q5153359.
    ?area p:P6 ?headRole ; 
        p:P131 ?superiorArea .
    ?headRole p:P6 ?person ;
        p:P580 ?headRoleStartDate ;
        p:P582 ?headRoleEndDate .
    ?person p:P102 ?partyRole .
    ?partyRole p:P580 ?partyRoleStartDate ;
            p:P582 ?partyRoleEndDate ;
            p:P102 ?party .
    ?party p:P1387 ?alignmentLabel .
}
WHERE {
    {
        ?area wdt:P31 wd:Q38911 .
    } UNION {
        ?area wdt:P31|p:P31 wd:Q5153359 .
        OPTIONAL {
            ?area wdt:P131|p:P131 ?superiorArea
        }
    } UNION {
        wd:Q3342946 wdt:P1269 ?area
    }
    OPTIONAL {
        ?area p:P6|wdt:P6 ?headRole .
        ?headRole ps:P6 ?person ;
            pqv:P580|pq:P580 ?headRoleStartDate .
        OPTIONAL {
            ?headRole pqv:P582|pq:P582 ?headRoleEndDate .
        }
        OPTIONAL {
            ?person p:P102|wdt:102 ?partyRole .
            OPTIONAL {
                ?partyRole pqv:P580|pq:P580 ?partyRoleStartDate .
            }
            OPTIONAL {
                ?partyRole pqv:P582|pq:P582 ?partyRoleEndDate .
            }
            OPTIONAL {
                ?partyRole ps:P102 ?party .
                ?party wdt:P1387|p:P1387 ?alignment .
                ?alignment rdfs:label ?alignmentLabel
                FILTER (lang(?alignmentLabel) = "en")
            }
        }
    }
}
\end{lstlisting}


\begin{lstlisting}[language = SPARQL, caption={Creating the \textbf{appliesToRefPeriod} triples}, label={wdapplies},captionpos=b, escapeinside={(*@}{@*)}]
PREFIX cssa-ody:  <https://data.cssz.cz/ontology/days/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dt: <http://kizi.vse.cz/novp19/diploma-thesis/>
    
CONSTRUCT {
    ?headRole dt:appliesToRefPeriod ?refPeriod .
    ?partyRole dt:appliesToRefPeriod ?refPeriod .
}
WHERE {
    GRAPH <https://data.cssz.cz/resource/dataset/pomocne-ciselniky> {
        ?refPeriod skos:inScheme cssa-ody:DaysScheme .
        FILTER REGEX(str(?refPeriod), ".*year.*")
        FILTER REGEX(str(?refPeriod), "^.*\\d{4}-12-31")
        BIND(REPLACE(str(?refPeriod), "^.*(\\d{4}).*","$1") as ?refPeriodBind)
    }
    ?area p:P6 ?headRole .
    ?headRole p:P580 ?headRoleStartDate .
    FILTER ( datatype(?headRoleStartDate) = xsd:string)	
    BIND(REPLACE(str(?headRoleStartDate), "^(\\d{4}).*","$1") as ?headRoleStartDateBind)
    OPTIONAL {
        ?headRole p:P582 ?headRoleEndDate .
        FILTER ( datatype(?headRoleEndDate) = xsd:string)	
        BIND(REPLACE(str(?headRoleEndDate), "^(\\d{4}).*","$1") as ?headRoleEndDateBind)
    }
    FILTER (
        (bound(?headRoleEndDateBind) && ?refPeriodBind >= ?headRoleStartDateBind && ?refPeriodBind <= ?headRoleEndDateBind)
        ||
        (!bound(?headRoleEndDateBind) && ?refPeriodBind >= ?headRoleStartDateBind && ?headRoleStartDateBind > "2000")
    )
    OPTIONAL {
        ?headRole p:P6 ?person .
        ?person p:P102 ?partyRole .
        OPTIONAL {
            ?partyRole p:P580 ?partyRoleStartDate .
            FILTER (datatype(?partyRoleStartDate) = xsd:string)	
            BIND(REPLACE(str(?partyRoleStartDate), "^(\\d{4}).*","$1") as ?partyRoleStartDateBind)
        }
        OPTIONAL {
            ?partyRole p:P582 ?partyRoleEndDate .
            FILTER ( datatype(?partyRoleEndDate) = xsd:string)	
            BIND(REPLACE(str(?partyRoleEndDate), "^(\\d{4}).*","$1") as ?partyRoleEndDateBind)
        }
        FILTER (
            (bound(?partyRoleEndDateBind) && ?refPeriodBind >= ?partyRoleStartDateBind && ?refPeriodBind <= ?partyRoleEndDateBind)
            ||
            (!bound(?partyRoleEndDateBind) && ?refPeriodBind >= ?partyRoleStartDateBind && ?partyRoleStartDateBind > "2000")
            ||
            (!bound(?partyRoleStartDateBind) && !bound(?partyRoleEndDateBind))
        )
    }
}
\end{lstlisting}